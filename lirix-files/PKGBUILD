pkgname=lirix-files
pkgver=1
pkgrel=1
pkgdesc='Branding files for Lirix + Filesystem generator'
arch=('x86_64')
license=('BSD3')
source=('crypttab' 'env-generator' 'fstab' 'group' 'gshadow' 'host.conf' 'hosts'
        'issue' 'ld.so.conf' 'locale.sh' 'motd' 'nsswitch.conf' 'os-release'
        'passwd' 'profile' 'resolv.conf' 'securetty' 'shadow' 'shells' 'sysctl'
        'sysusers' 'tmpfiles')
sha512sums=('SKIP')

package() {
	cd "$pkgdir"
	# Setup root filesystem
	for d in boot dev etc mnt usr usr/people var opt srv/http run; do
		install -d -m755 $d
	done
	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	install -d -m555 -g 11 srv/ftp

	# Setup /etc and /usr/share/factory/etc
	install -d etc/{ld.so.conf.d,skel,profile.d} usr/share/factory/etc
	for f in fstab group host.conf hosts issue ld.so.conf motd nsswitch.conf \
		passwd resolv.conf securetty shells profile; do
		install -m644 "$srcdir"/$f etc/
		install -m644 "$srcdir"/$f usr/share/factory/etc/
	done
	ln -s ../proc/self/mounts etc/mtab
	for f in gshadow shadow crypttab; do
		install -m600 "$srcdir"/$f etc/
		install -m600 "$srcdir"/$f usr/share/factory/etc/
	done
	touch etc/arch-release
	install -m644 "$srcdir"/locale.sh etc/profile.d/locale.sh
	install -Dm644 "$srcdir"/os-release usr/lib/os-release

	# Setup /var
	for d in cache local opt log/old lib/misc empty; do
		install -d -m755 var/$d
	done
	install -d -m1777 var/{tmp,spool/mail}

	# Allow setgid games (gid 50) to write scores
	install -d -m775 -g 50 var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	# Setup /usr hierarchy
	for d in bin include lib share/{misc,pixmaps} src; do
		install -d -m755 usr/$d
	done
	for d in {1..8}; do
		install -d -m755 usr/share/man/man$d
	done

	# Add lib symlinks
	ln -s usr/lib lib
	[[ $CARCH = 'x86_64' ]] && {
		ln -s usr/lib lib64
		ln -s lib usr/lib64
		}

	# Add bin symlinks
	ln -s usr/bin bin
	ln -s usr/bin sbin
	ln -s bin usr/sbin

	# Add /usr/people symlink
	ln -s home usr/people

	# Setup /usr/local hierarchy
	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/$d
	done
	ln -s ../man usr/local/share/man

	# Setup systemd-sysctl
	install -D -m644 "$srcdir"/sysctl usr/lib/sysctl.d/10-arch.conf

	# Setup systemd-sysusers
	install -D -m644 "$srcdir"/sysusers usr/lib/sysusers.d/arch.conf

	# Setup systemd-tmpfiles
	install -D -m644 "$srcdir"/tmpfiles usr/lib/tmpfiles.d/arch.conf

	# Setup systemd.environment-generator
	install -D -m755 "$srcdir"/env-generator usr/lib/systemd/system-environment-generators/10-arch
	
	install -Dm644 "$srcdir"/lirix.png usr/share/pixmaps/lirix.png
	install -m644 "$srcdir"/lirix-logo.png usr/share/pixmaps/lirix-logo.png 
}
